/**
 * References:
 * https://github.com/apereo/cas-gradle-overlay-template/blob/master/cas/build.gradle
 * https://github.com/scalding/gradle-waroverlay-plugin
 */
buildscript {
    repositories {
        mavenLocal()
        // If using a Maven proxy, put the property settings in
        // ~/.gradle/gradle.properties for default_proxy_url,
        // default_proxy_username and default_proxy_password.
        if (project.hasProperty("default_proxy_url")) {
            maven {
                url project.property("default_proxy_url")
                credentials {
                    username project.property("default_proxy_username")
                    password project.property("default_proxy_password")
                }
            }
        }
        jcenter()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.5.9.RELEASE"
        classpath "org.scaldingspoon.gradle:gradle-waroverlay-plugin:0.9.3"
    }
}

version projectVersion
group projectGroup

apply plugin: 'org.springframework.boot'
apply from: "https://raw.githubusercontent.com/apereo/cas/${project.casGitBranch}/gradle/overrides.gradle"
apply plugin: 'war'
apply plugin: 'application'
apply plugin: 'waroverlay'

repositories {
    mavenLocal()
    // If using a Maven proxy, put the property settings in
    // ~/.gradle/gradle.properties for default_proxy_url,
    // default_proxy_username and default_proxy_password.
    if (project.hasProperty("default_proxy_url")) {
        maven {
            url project.property("default_proxy_url")
            credentials {
                username project.property("default_proxy_username")
                password project.property("default_proxy_password")
            }
        }
    }
    jcenter()
}

dependencies {
    compile("org.apereo.cas:cas-server-webapp:${project.casVersion}@war") {
        transitive = true
    }
    runtime "org.apereo.cas:cas-server-support-yaml-service-registry:${project.casVersion}"
    runtime "org.apereo.cas:cas-server-support-ldap:${project.casVersion}"

    runtime group: 'com.fasterxml', name: 'classmate', version: '1.3.4'
    providedRuntime "org.springframework.boot:spring-boot-starter-tomcat:1.5.9.RELEASE"
}

springBoot {
    mainClass = "org.springframework.boot.loader.WarLauncher"
}
mainClassName = "org.springframework.boot.loader.WarLauncher"

bootRepackage {
    mainClass = "org.apereo.cas.web.CasWebApplication"
    executable = true
}

bootRun {
  addResources = true
  jvmArgs = [
      "-Dserver.contextPath=/cas",
      "-Dserver.port=8060",
      "-Dserver.ssl.keyStore=${projectDir}/cas/testkeystore.jks",
      "-Dserver.ssl.keyStorePassword=changeit",
      "-Dserver.ssl.keyPassword=changeit",
      "-Dlogging.config=classpath:log4j2.xml",
      "-Dcas.standalone.config=${projectDir}/cas",
      "-Dcas.standalone.config.file=${projectDir}/cas/cas_config.yml"
  ]
}
